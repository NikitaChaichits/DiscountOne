/**
 * This file defines configuration for all project
 */

ext {

    def keysProperties = new Properties()
    keysProperties.load(new FileInputStream(rootProject.file("keys.properties")))

    /**
     * Common configuration for all project
     */

    globalConfig = [
            compileSdkVersion: 34,
            minSdkVersion    : 26,
            targetSdkVersion : 34,
            buildToolsVersion: "34.0.0"
    ]

    /**
     * Configuration for app module
     */

    def appVersionMajor = 1
    def appVersionPatch = 5
    def appVersionBuild = 3

    appConfig = [
            applicationId: 'com.digeltech.discountone',

            versionName  : generateVersionName(appVersionMajor, appVersionPatch, appVersionBuild),
            versionCode  : generateVersionCode(appVersionMajor, appVersionPatch, appVersionBuild),
            versionBuild : appVersionBuild,

            keyAlias     : keysProperties['keyAlias'],
            keyPassword  : keysProperties['keyPassword'],
            storeFile    : file(keysProperties['storeFile']),
            storePassword: keysProperties['storePassword'],
    ]
}

/**
 * Versions calculations
 */

static Integer generateVersionCode(versionMajor, versionPatch, versionBuild) {
    return versionMajor * 100 + versionPatch * 10 + versionBuild
}

static String generateVersionName(versionMajor, versionPatch, versionBuild) {
    return "${versionMajor}.${versionPatch}.${versionBuild}"
}
